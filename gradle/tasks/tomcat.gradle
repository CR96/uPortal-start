repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    tomcat
    shared
}

dependencies {
    tomcat "org.apache.tomcat:tomcat:${tomcatVersion}@tar.gz"
    shared "javax.ccpp:ccpp:${ccppVersion}"
    shared "org.apache.portals.pluto:pluto-container-api:${plutoVersion}"
    shared "org.apache.portals.pluto:pluto-container-driver-api:${plutoVersion}"
    shared "org.apache.portals.pluto:pluto-taglib:${plutoVersion}"
    shared "org.jasig.service.persondir:person-directory-api:${personDirectoryVersion}"
    shared "org.jasig.portal:uPortal-api-platform:${uPortalVersion}"
    shared "${portletApiDependency}"
}

task openDefaultUrlInBrowser() {
   group 'Tomcat'
   description 'Opens the Default URL in a browser'

   doLast {
      java.awt.Desktop.desktop.browse 'http://localhost:8080/uPortal/'.toURI()
   }
}

task tomcatInstall() {
    description 'Downloads the Apache Tomcat servlet container and performs the necessary configuration steps'

    doLast {
        String serverHome = rootProject.ext['buildProperties'].getProperty('server.home')

        logger.lifecycle("Installing Tomcat servlet container version ${tomcatVersion} to location ${serverHome}")

        File tomcatTmpDir = new File('build/tmp/tomcat')

        // Clear & reset the tomcat directory
        tomcatTmpDir.deleteDir()
        tomcatTmpDir.mkdirs()

        File tomcatTarFile = new File("${tomcatTmpDir}/tomcat-gunziped.tar")

        // Gunzip the Tomcat binaries
        File tomcatTarGz = configurations.tomcat.files.iterator().next()
        ant.gunzip(src: tomcatTarGz, dest: tomcatTarFile.getPath())

        // Un-archive the tar file
        copy {
            from tarTree(tomcatTarFile.getPath())
            into tomcatTmpDir
        }

        // Clear any previous copy of Tomcat
        new File(serverHome).deleteDir()

        // Copy to the final location & configure our settings in the process
        copy {
            from "${tomcatTmpDir}/apache-tomcat-${tomcatVersion}"
            into serverHome

            // Configure 'shared.loader' property
            filesMatching('conf/catalina.properties') {
                filter { line ->
                    line.replace('shared.loader=', 'shared.loader=${catalina.base}/shared/lib/*.jar')
                }
            }

            // Configure sessionCookiePath attribute
            filesMatching('conf/context.xml') {
                filter { line ->
                    line.replace('<Context>', '<Context sessionCookiePath="/">')
                }
            }
        }

        // Add setenv.sh (*nix) & setenv.bat (Windows)
        copy {
            from 'etc/tomcat/bin'
            into "${serverHome}/bin"
        }

        // Add shared.loader dependencies
        copy {
            from configurations.shared
            into "${serverHome}/shared/lib"
        }
    }
}

task tomcatStart() {
    group 'Tomcat'
    description 'Start the embedded Tomcat servlet container'

    doLast {
        String serverHome = rootProject.ext['buildProperties'].getProperty('server.home')
        logger.lifecycle("Starting Tomcat servlet container in ${serverHome}")
        String executable = isWindows ? 'cmd' : './startup.sh'
        ant.exec(dir: "${serverHome}/bin", executable: executable, spawn: true) {
            if (isWindows) {
                arg(value: '/c')
                arg(value: 'startup.bat')
            }
        }
    }
}

task tomcatStop() {
    group 'Tomcat'
    description 'Stop the embedded Tomcat servlet container'

    doLast {
        String serverHome = rootProject.ext['buildProperties'].getProperty('server.home')
        logger.lifecycle("Stopping Tomcat servlet container in ${serverHome}")
        String executable = isWindows ? 'cmd' : './shutdown.sh'
        ant.exec(dir: "${serverHome}/bin", executable: executable, spawn: true) {
            if (isWindows) {
                arg(value: '/c')
                arg(value: 'shutdown.bat')
            }
        }
    }
}

task tomcatClearLogs(type: Delete) {
    group 'Tomcat'
    description 'Delete all log files within Tomcat'

    doFirst {
        String serverBase = rootProject.ext['buildProperties'].getProperty('server.base')
        FileTree logsFolder = fileTree("${serverBase}/logs")
        if (logsFolder.isEmpty()) {
            logger.lifecycle('The Tomcat logs directory is empty;  there is noting to delete')
        } else {
            logsFolder.visit { FileVisitDetails details ->
                logger.lifecycle("Deleting ${details.name}")
                delete details.file
            }
        }
    }
}
