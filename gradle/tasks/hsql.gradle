repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    hsqldb
}

dependencies {
    hsqldb "org.hsqldb:hsqldb:${hsqldbVersion}"
    hsqldb "org.hsqldb:sqltool:${hsqldbVersion}"  // For stopping the embedded HSQLDB
}

task hsqlStart {
    group 'HSQL'
    description 'Start the embedded hsql server'

    doLast {
        logger.lifecycle('Starting embedded HSQLDB uPortal database')

        String classpath = '';
        configurations.hsqldb.resolve().each {
            if (classpath.length() != 0) {
                classpath += ':'
            }
            classpath += it.getAbsolutePath()
        }

        ant.exec(executable: 'java', spawn: true) {
            arg(value: '-cp')
            arg(value: classpath)
            arg(value: 'org.hsqldb.server.Server')
            arg(value: '--database.0')
            arg(value: 'file:./.gradle/hsqldb/uPortal;hsqldb.tx=mvcc')
            arg(value: '--dbname.0')
            arg(value: 'uPortal')
            arg(value: '--address')
            arg(value: 'localhost')
            arg(value: '--port')
            arg(value: '8887')
        }
    }
}

task hsqlStop {
    group 'HSQL'
    description 'Stop the embedded hsql server'

    doLast {
        logger.lifecycle('Stopping embedded HSQLDB uPortal database')

        String classpath = '';
        configurations.hsqldb.resolve().each {
            if (classpath.length() != 0) {
                classpath += ':'
            }
            classpath += it.getAbsolutePath()
        }

        ant.exec(executable: 'java', spawn: true) {
            arg(value: '-cp')
            arg(value: classpath)
            arg(value: 'org.hsqldb.cmdline.SqlTool')
            arg(value: '--rcFile')
            arg(value: 'etc/hsql/hsqldb.sqltool.rc')
            arg(value: '--sql')
            arg(value: 'shutdown compact;')
            arg(value: 'uPortalDb')
        }
    }
}
